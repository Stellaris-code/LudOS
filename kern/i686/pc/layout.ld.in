ENTRY(_start)

SECTIONS
{
    . = @KERNEL_VIRTUAL_BASE@ + 1M; /* + 1M */

    .boot ALIGN(0x1000) : AT(ADDR(.boot) - @KERNEL_VIRTUAL_BASE@)
    {
        mbd = .;
        *(.multiboot_header)
    }

    _load_start = .;

    .text ALIGN(0x1000) : AT(ADDR(.text) - @KERNEL_VIRTUAL_BASE@)
    {
        *(.text)
        *(.gnu.linkonce.t*)
    }

    .rodata ALIGN(0x1000) : AT(ADDR(.rodata) - @KERNEL_VIRTUAL_BASE@)
    {
        start_ctors = .;
        *(SORT(.ctors*))  /* Note the "SORT" */
        end_ctors = .;

        start_dtors = .;
        *(SORT(.dtors*))
        end_dtors = .;

        *(.rodata*)
        *(.gnu.linkonce.r*)
    }

    /* For stack unwinding (exception handling)  */
    .eh_frame_hdr ALIGN(0x8): AT(ADDR(.eh_frame_hdr) - @KERNEL_VIRTUAL_BASE@)
    {
      KEEP(*(.eh_frame_hdr*))
    }
    .eh_frame ALIGN(0x8): AT(ADDR(.eh_frame) - @KERNEL_VIRTUAL_BASE@)
    {
      PROVIDE (__eh_frame_start = .);
      KEEP(*(.eh_frame))
      LONG (0);
    }

    .gcc_except_table : AT(ADDR(.gcc_except_table) - @KERNEL_VIRTUAL_BASE@)
    {
      *(.gcc_except_table)
    }

    .data ALIGN(0x1000) : AT(ADDR(.data) - @KERNEL_VIRTUAL_BASE@)
    {
        data_seg = .;
        *(.data)
        *(.gnu.linkonce.d*)
    }

    _load_end = .;

    .bss : AT(ADDR(.bss) - @KERNEL_VIRTUAL_BASE@)
    {
        _bss_start = .;
        *(COMMON)
        *(.bss)
        _bss_end = .;
    }


    . = ALIGN(0x1000);

    /DISCARD/ :
    {
        *(.comment)
    }   


    kernel_virtual_end = .;
    kernel_physical_end = . - @KERNEL_VIRTUAL_BASE@;
}
